apply plugin: 'com.android.application'

android {
    compileSdkVersion 33


    defaultConfig {
        applicationId 'io.github.ferreol.seteditplus'
        minSdkVersion 21
        //noinspection ExpiredTargetSdkVersion
        targetSdkVersion 22
        versionCode 1
        versionName "1"
        resConfigs 'en'
    }

    signingConfigs {
        debug {
            storeFile file('dev_keystore.jks')
            storePassword 'kJCp!Bda#PBdN2RLK%yMK@hatq&69E'
            keyPassword 'kJCp!Bda#PBdN2RLK%yMK@hatq&69E'
            keyAlias 'key0'
        }
    }

    externalNativeBuild {
        cmake {
            path "CMakeLists.txt"
            version "3.10.2"
        }
    }

    splits {
        abi {
            enable false
            reset()
            include 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
            universalApk true
        }
    }

    def archPrefix = ['armeabi-v7a':4, 'arm64-v8a':2, 'x86':3, 'x86_64':1]

    android.applicationVariants.all { variant ->
        variant.outputs.each { output ->
            int abiVersionCode = archPrefix.get(output.getFilter(com.android.build.OutputFile.ABI), 0)
            output.versionCodeOverride = (abiVersionCode * 100000) + defaultConfig.versionCode
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            applicationIdSuffix '.debug'
            signingConfig signingConfigs.debug
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    namespace 'io.github.ferreol.seteditplus'
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.6.0'
    implementation 'com.google.android.material:material:1.7.0'
}